apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "io.github.josephdalughut.journal.android"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName "0.1.0" //use semantic versioning
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            // Run code coverage reports by default on debug builds.
            // testCoverageEnabled = true
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    flavorDimensions "mock"

    /*
        We'll have two product flavors:
        - mock for "mock" data used in development
        - prod for production which would be used by users
     */
    productFlavors {
        mock {
//            applicationIdSuffix = ".mock"
            dimension "mock"
        }
        prod {}
    }

    // We don't need mockRelease, let's remove it
    android.variantFilter { variant ->
        if (variant.buildType.name.equals('release')
                && variant.getFlavors().get(0).name.equals('mock')) {
            variant.setIgnore(true)
        }
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //android ui libs
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'

    //let's use butterkinfe for field/method binding, calling 'findViewById(...)' all the time gets exhausting
    implementation 'com.jakewharton:butterknife:8.8.1'
    implementation 'com.google.android.gms:play-services-maps:15.0.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    //image loading
    implementation 'com.squareup.picasso:picasso:2.5.2'

    // Unit testing using JUnit and Mockito for mocking
    implementation 'com.google.code.findbugs:jsr305:3.0.1'
    testImplementation "junit:junit:4.12"
    testImplementation "org.mockito:mockito-all:1.10.19"

    // Robolectric for TDD, unit testing individual Android components
    testImplementation "org.robolectric:robolectric:3.8"
    testImplementation "org.robolectric:shadows-supportv4:3.8"

    // Android Testing Support Library's runner and rules
    androidTestImplementation "com.android.support.test:runner:1.0.2"
    androidTestImplementation "com.android.support.test:rules:1.0.2"

    // Espresso UI Testing dependencies.
    androidTestImplementation "com.android.support.test.espresso:espresso-core:3.0.2"
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:3.0.2"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:3.0.2"

    //Local data persistence
    def room_version = "1.1.1"
    implementation "android.arch.persistence.room:runtime:$room_version"
    annotationProcessor "android.arch.persistence.room:compiler:$room_version"

    // Room Test helpers
    testImplementation "android.arch.persistence.room:testing:$room_version"

    //Paging
    def paging_version = "1.0.1"
    implementation "android.arch.paging:runtime:$paging_version"

    // alternatively - without Android dependencies for testing
    testImplementation "android.arch.paging:common:$paging_version"

    // ViewModel and LiveData
    def lifecycle_version = "1.1.1"
    implementation "android.arch.lifecycle:extensions:$lifecycle_version"

    //Firebase
    implementation 'com.google.firebase:firebase-core:16.0.1'
    implementation 'com.google.firebase:firebase-firestore:17.0.2'
    implementation 'com.google.firebase:firebase-auth:16.0.2'
    implementation 'com.google.android.gms:play-services-auth:15.0.1'


    implementation 'com.android.support:multidex:1.0.3'

}

apply plugin: 'com.google.gms.google-services'
